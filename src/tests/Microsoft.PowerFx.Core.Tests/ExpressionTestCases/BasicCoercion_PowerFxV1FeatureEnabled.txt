#SETUP: PowerFxV1CompatibilityRules

>> "Hello " & 42
"Hello 42"

>> 42 & "Hello"
"42Hello"

// Arithmetic operations coerce string to number
>> "2" + 3
5

>> "2" + "3"
5

>> "2" * "3"
6

// non-numeric Strings don't coorce
>> ("x" * "3")
Error({Kind:ErrorKind.InvalidArgument})

// bools coorce to number as 1 or 0
>> true - false
1

>> 1 + Blank()
1

// can coorce number to bool via logical operators
>> 7 && 0
false

>> 0 || 7
true

// can coorce text to bool via logical operators
>> "true" && "anything"
Error({Kind:ErrorKind.InvalidArgument})

>> "anything" || "true"
Error({Kind:ErrorKind.InvalidArgument})

// blank coorces to false via logical operators
>> true && !Blank()
true

// Erroneous Date -> Bool coercion
>> Date(2000,1,2) && 1
Errors: Error 0-14: Invalid argument type. Expecting one of the following: Boolean, Number, Decimal, Text, OptionSetValue, UntypedObject.

>> If(Date(2000,1,1),1,2)
Errors: Error 3-17: Invalid argument type (Date). Expecting a Boolean value instead.

>> GUID("AAAAAAAA-BBBB-CCCC-DDDD-EEEEFFFFFFFF") & " is a GUID"
"aaaaaaaa-bbbb-cccc-dddd-eeeeffffffff is a GUID"

>> If(1<0, GUID("AAAAAAAA-BBBB-CCCC-DDDD-EEEEFFFFFFFF"), "11111111-2222-3333-4444-555566666666")
GUID("11111111-2222-3333-4444-555566666666")

>> If(1<0, Time(1,2,3), Date(2000,1,1))
Time(0,0,0,0)

>> If(1<0, Time(1,2,3), DateTime(2000,1,1,12,34,56,789))
Time(12,34,56,789)

>> If(1<0, Date(2000,1,1), Time(12,0,0))
Date(1899,12,30)

>> If(1<0, Date(2000,1,1), DateTime(2134,5,6,12,0,0))
Date(2134,5,6)

>> If(1<0, DateTime(2000,1,1,1,1,1), Time(12,34,56,789))
DateTime(1899,12,30,12,34,56,789)

>> If(1<0, DateTime(2000,1,1,1,1,1), Date(2023,3,27))
DateTime(2023,3,27,0,0,0,0)
