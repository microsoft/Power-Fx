#SETUP: DecimalSupport
#SETUP: PowerFxV1CompatibilityRules

// There is no Patch_V1CompatDisabled.txt because running with disable:PowerFxV1CompatilityRules has 
// many problems in the interpreter and not worth pursuing.

// basic mutations with blanks and coercions

>> Set(y, [{d:1,t:"hi",b:true},{d:2,t:"bye",b:false},{d:3,t:"cycle",b:Blank()}] )
Table({b:true,d:1,t:"hi"},{b:false,d:2,t:"bye"},{b:Blank(),d:3,t:"cycle"})

>> 0;y
Table({b:true,d:1,t:"hi"},{b:false,d:2,t:"bye"},{b:Blank(),d:3,t:"cycle"})

>> Patch( y, LookUp( y, d=2 ), {t:"back"})
{b:false,d:2,t:"back"}

>> 1;y
Table({b:true,d:1,t:"hi"},{b:false,d:2,t:"back"},{b:Blank(),d:3,t:"cycle"})

>> Patch( y, First(y), {b:Blank()})
{b:Blank(),d:1,t:"hi"}

>> 2;y
Table({b:Blank(),d:1,t:"hi"},{b:false,d:2,t:"back"},{b:Blank(),d:3,t:"cycle"})

>> Patch( y, LookUp(y, b=Blank()), {b:true})
{b:true,d:1,t:"hi"}

>> 3;y
Table({b:true,d:1,t:"hi"},{b:false,d:2,t:"back"},{b:Blank(),d:3,t:"cycle"})

>> Patch( y, Index(y,2), {b:Blank(),d:12,t:"different"})
{b:Blank(),d:12,t:"different"}

>> 4;y
Table({b:true,d:1,t:"hi"},{b:Blank(),d:12,t:"different"},{b:Blank(),d:3,t:"cycle"})

>> Patch( y, Index(y,2), {b:1,d:"23",t:DateTime(2002,2,2,2,2,2)})
Errors: Error 22-61: The type of this argument 'd' does not match the expected type 'Decimal'. Found type 'Text'.|Error 0-5: The function 'Patch' has some invalid arguments.

>> 5;y
Table({b:true,d:1,t:"hi"},{b:Blank(),d:12,t:"different"},{b:Blank(),d:3,t:"cycle"})

>> Patch( y, Index(y,3), {b:"0",d:true,t:GUID("1d9ec9bf-3b8b-4ff3-bac4-c2a723a0f49c")} )
Errors: Error 22-83: The type of this argument 'b' does not match the expected type 'Boolean'. Found type 'Text'.|Error 0-5: The function 'Patch' has some invalid arguments.

// should not have been changed from the previous patch
>> 6;y
Table({b:true,d:1,t:"hi"},{b:Blank(),d:12,t:"different"},{b:Blank(),d:3,t:"cycle"})

>> Patch( y, Index(y,3), {b:false,d:1/0,t:GUID("1d9ec9bf-3b8b-4ff3-bac4-c2a723a0f49d")} )
{b:false,d:Error({Kind:ErrorKind.Div0}),t:"1d9ec9bf-3b8b-4ff3-bac4-c2a723a0f49d"}

// deep mutation with coercions

>> Set(x, { v :1 , t: [1,2,3]})
{t:Table({Value:1},{Value:2},{Value:3}),v:1}

>> 0;x.t
Table({Value:1},{Value:2},{Value:3})

>> Patch(x.t, Index(x.t,2), { Value: 99})
{Value:99}

>> 1;x.t
Table({Value:1},{Value:99},{Value:3})

>> Patch(x.t, {Value:99}, {Value:88})
{Value:88}

>> 2;x.t
Table({Value:1},{Value:88},{Value:3})

>> Patch(x.t, {}, {Value:77})
{Value:77}

>> 3;x.t
Table({Value:1},{Value:88},{Value:3},{Value:77})

>> Patch(x.t, {Value:88}, {Value:"77"})
Errors: Error 23-35: The type of this argument 'Value' does not match the expected type 'Decimal'. Found type 'Text'.|Error 0-5: The function 'Patch' has some invalid arguments.

>> 4;x.t
Table({Value:1},{Value:88},{Value:3},{Value:77})

>> Patch(x.t, {Value:88}, {Value:Float(66)})
{Value:66}

>> 5;x.t
Table({Value:1},{Value:66},{Value:3},{Value:77})

>> Patch(x.t, First(x.t), {Value:false})
{Value:0}

>> 6;x.t
Table({Value:0},{Value:66},{Value:3},{Value:77})

>> Set(j, [{d:1,t:"hi",b:true},{d:2,t:"bye",b:false},{d:3,t:GUID("1d9ec9bf-3b8b-4ff3-bac4-c2a723a0f49d"),b:Blank()}] )
Table({b:true,d:1,t:"hi"},{b:false,d:2,t:"bye"},{b:Blank(),d:3,t:"1d9ec9bf-3b8b-4ff3-bac4-c2a723a0f49d"})

>> Patch( j, Index(j,3), {b:true,d:1,t:GUID("1d9ec9bf-0000-0000-0000-c2a723a0f49d")} )
{b:true,d:1,t:"1d9ec9bf-0000-0000-0000-c2a723a0f49d"}

>> 0;j
Table({b:true,d:1,t:"hi"},{b:false,d:2,t:"bye"},{b:true,d:1,t:"1d9ec9bf-0000-0000-0000-c2a723a0f49d"})

