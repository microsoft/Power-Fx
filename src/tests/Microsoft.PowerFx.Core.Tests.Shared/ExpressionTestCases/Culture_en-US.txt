#SETUP: RegEx,CultureInfo("en-US"),PowerFxV1CompatibilityRules,ConsistentOneColumnTableResult

// Four types of letter I
//        Dotted     Dotless
// Upper  İ U+0130   I U+0049
// Lower  i U+0069   ı U+0131

>> Language()
"en-US"

>> "İ" = UniChar( Hex2Dec( "0130") )
true

>> "ı" = UniChar( Hex2Dec( "0131" ) )
true

// UPPER, LOWER, PROPER

>> Upper( "i" )
"I"

>> Lower( "I" )
"i"

>> Upper( "i" ) = "I"
true

>> Lower( "I" ) = "i"
true

>> Lower( "quit" ) = Lower( "QUIT" )
true

>> Lower( "quit" ) = Lower( "QUİT" )
true

>> Lower( "quıt" ) = Lower( "QUIT" )
false

>> Upper( "quit" ) = Upper( "QUIT" )
true

>> Proper( "Iabc" )
"Iabc"

>> Proper( "iabc" )
"Iabc"

// VALUE, DECIMAL, FLOAT

>> Value( "123,456" )
123456

>> Value( "123,456", "tr-TR" )
123.456

>> Decimal( "123,456" )
123456

>> Decimal( "123,456", "tr-TR" )
123.456

>> Float( "123,456" )
123456

>> Float( "123,456", "tr-TR" )
123.456

// TEXT

>> Text( DateTime(2010,1,1,14,0,0,0), "mmm ddd yyyy AM/PM" )
"Jan Fri 2010 PM"

>> Text( DateTime(2020,1,1,2,0,0,0), "mmmm dddd yyyy AM/PM" )
"January Wednesday 2020 AM"

>> Text( 123456789, "#,###.00" )
"123,456,789.00"

>> Text( 123456789, "#.###,00" )
"123456789.00000"

// IN AND EXACTIN

>> "i" in "SIGH"
true

>> "I" in "sigh"
true

>> "i" exactin "SIGH"
false

>> "I" exactin "sigh"
false

>> "I" exactin "SIGH"
true

>> "i" exactin "sigh"
true

>> "sIGh" in ["sigh","bcde"]
true

>> "siGh" in ["SIGH","bcde"]
true

>> "sIGH" in ["sigh","bcde"]
true

>> "siGH" in ["bcde","sIgh"]
true

>> "SIgh" in ["bcde","sigh"]
true

// SORT
// Relative order of i, I, ı, İ are different between en-US and tr-TR

>> Sort( [ "Z", "İ", "z", "I", "J", "j", "ı", "a", "h", "i", "A", "H"], Value )
Table({Value:"a"},{Value:"A"},{Value:"h"},{Value:"H"},{Value:"i"},{Value:"I"},{Value:"İ"},{Value:"ı"},{Value:"j"},{Value:"J"},{Value:"z"},{Value:"Z"})

>> SortByColumns( [ "Z", "İ", "z", "I", "J", "j", "ı", "a", "h", "i", "A", "H"], "Value" )
Table({Value:"a"},{Value:"A"},{Value:"h"},{Value:"H"},{Value:"i"},{Value:"I"},{Value:"İ"},{Value:"ı"},{Value:"j"},{Value:"J"},{Value:"z"},{Value:"Z"})

>> Concat( Sort( Split( "j J k K l L m M n N o O p P r R s S t T u U v V y Y z Z Ç ç Ş ş Ü ü Ö ö İ ı Ğ ğ a A b B c C d D e E f F g G h H i I", " " ), Value ), Value, " " )
"a A b B c C ç Ç d D e E f F g G ğ Ğ h H i I İ ı j J k K l L m M n N o O ö Ö p P r R s S ş Ş t T u U ü Ü v V y Y z Z"

>> Concat( SortByColumns( Split( "d D e E f F g G h H i I j J k K l L m M n N o O p P r R s S t T u U v V y Y z Z Ç ç Ş ş Ü ü Ö ö İ ı Ğ ğ a A b B c C", " " ), "Value" ), Value, " " )
"a A b B c C ç Ç d D e E f F g G ğ Ğ h H i I İ ı j J k K l L m M n N o O ö Ö p P r R s S ş Ş t T u U ü Ü v V y Y z Z"

// REGULAR EXPRESSIONS
// Always uses invariant even though tr-TR is set, subject of https://github.com/microsoft/Power-Fx/issues/2538

// Results when using C#                                  // Invariant   tr-TR    en-US

>> IsMatch( "İ", "i", MatchOptions.IgnoreCase )           // false       TRUE     TRUE
false

>> IsMatch( "i", "İ", MatchOptions.IgnoreCase )           // false       TRUE     TRUE
false

>> IsMatch( "ı", "I", MatchOptions.IgnoreCase )           // false       TRUE     false
false

>> IsMatch( "I", "ı", MatchOptions.IgnoreCase )           // false       TRUE     false
false

>> IsMatch( "İ", "I", MatchOptions.IgnoreCase )           // false       false    TRUE
false

>> IsMatch( "I", "İ", MatchOptions.IgnoreCase )           // false       false    TRUE
false

>> IsMatch( "ı", "i", MatchOptions.IgnoreCase )           // false       false    false
false

>> IsMatch( "i", "ı", MatchOptions.IgnoreCase )           // false       false    false
false

>> IsMatch( "i", "I", MatchOptions.IgnoreCase )           // TRUE        false    TRUE
true

>> IsMatch( "I", "i", MatchOptions.IgnoreCase )           // TRUE        false    TRUE
true

>> IsMatch( "ı", "İ", MatchOptions.IgnoreCase )           // false       false    false
false

>> IsMatch( "İ", "ı", MatchOptions.IgnoreCase )           // false       false    false
false

>> Match( "hiIıİİıIhi", "\u0130+" )
{FullMatch:"İİ",StartMatch:5,SubMatches:Table()}

>> IsMatch( "Sıgh", "\u0131", MatchOptions.Contains )
true
